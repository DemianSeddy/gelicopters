
&НаКлиенте
Процедура ОбновитьПодсветкуСинтаксиса(Команда)
	КодДляВыполнения.УстановитьФорматированнуюСтроку(ПолучитьРаскрашенныйТекст(КодДляВыполнения.ПолучитьТекст()));
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекст()
	
	ТекстЦикла = "";
	
	Для каждого стр Из ПараметрыЗапроса Цикл
		ТекстЦикла = ТекстЦикла+"//ПараметрыЗапроса." + стр.Значение + "
		|";
		//ТекстЦикла = ТекстЦикла+"//Объект.Параметры.НайтиСтроки(Новый Структура(""Имя"","""+стр.Значение+"""))[0].ЗначениеВФорме;
		//|";
	КонецЦикла; 
	
	ТекстЦикла = ТекстЦикла+"Для Каждого СтрокаРезультата Из ТаблицаРезультатов Цикл
	|";
	Если не ДляРегистра Тогда
		Если КолонкиРезультата.НайтиПоЗначению("Регистратор") <> Неопределено Тогда
			ТекстЦикла = ТекстЦикла+"	ОбъектСсылка = СтрокаРезультата.Регистратор.ПолучитьОбъект();
			|";			
		Иначе
			ТекстЦикла = ТекстЦикла+"	ОбъектСсылка = СтрокаРезультата.Ссылка.ПолучитьОбъект();
			|";
		КонецЕсли; 
	КонецЕсли; 
	
	ТекстЦикла = ТекстЦикла+"	//ОбъектСсылка.ОбменДанными.Загрузка = Истина;
	|";
	Для каждого КолонкаЗапроса Из КолонкиРезультата Цикл
		ТекстЦикла = ТекстЦикла + "	//ОбъектСсылка." + КолонкаЗапроса.Значение + Символы.ПС;
	КонецЦикла;
	
	Если ДляДокумента Тогда
		ТекстЦикла = ТекстЦикла + "	
		|	ОбъектСсылка.Записать();
		|	//ОбъектСсылка.Записать(РежимЗаписиДокумента.Запись);
		|	//ОбъектСсылка.Записать(РежимЗаписиДокумента.Проведение);
		|	//ОбъектСсылка.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		|";
	ИначеЕсли ДляРегистра Тогда 
		ТекстЦикла = ТекстЦикла + "	
		|	Мен = РегистрыСведений.АдресныйКлассификатор.СоздатьМенеджерЗаписи();
		|	//Мен = РегистрыНакопления.АдресныйКлассификатор.СоздатьМенеджерЗаписи();
		|	ЗаполнитьЗначенияСвойств(Мен,СтрокаРезультата); 
		|	Мен.Удалить();";
	Иначе
		
		ТекстЦикла = ТекстЦикла + "	
		|	ОбъектСсылка.Записать();";
	КонецЕсли;
	
	ТекстЦикла = ТекстЦикла + "
	|КонецЦикла;"+ Символы.ПС;
	
	КодДляВыполненияСтрока = ТекстЦикла;
	КодДляВыполнения.УстановитьФорматированнуюСтроку(ПолучитьРаскрашенныйТекст(КодДляВыполненияСтрока));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыЗапроса = Параметры.Параметры;
	КолонкиРезультата = Параметры.Колонки;
	КодДляВыполненияСтрока = Параметры.КодДляВыполнения;
	ДляСправочника = Истина;
	ДляРегистра = Ложь;
	ДляДокумента = Ложь;
	
	Если НЕ ЗначениеЗаполнено(КодДляВыполненияСтрока) Тогда
		ЗаполнитьТекст();
	Иначе
		КодДляВыполнения.УстановитьФорматированнуюСтроку(ПолучитьРаскрашенныйТекст(КодДляВыполненияСтрока));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОКЗавершение", ЭтотОбъект), "Вы уверены?", РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОКЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК тогда
		Закрыть(КодДляВыполнения.ПолучитьТекст());
	КонецЕсли;
	
КонецПроцедуры


#Область Раскраска
Функция ПолучитьРаскрашенныйТекст(Знач ТЗ) Экспорт 
	
	Цвет_КлючевыеСлова = WebЦвета.Красный;
	Цвет_Функции = WebЦвета.ТемноБордовый;
	Цвет_Комментарии = WebЦвета.Зеленый;
	Цвет_Параметр = WebЦвета.ЦианНейтральный;
	Цвет_Число = WebЦвета.Черный;
	Цвет_Текст = WebЦвета.Синий;
	
	СимволыНПП = Символы.НПП;
	
	ТЗ = СтрЗаменить(ТЗ, Символы.Таб, "    ");
	ТЗ = СтрЗаменить(ТЗ, СимволыНПП, " ");
	МассивФМС = Новый Массив;
	
	МассивСтрок = ИзСтрокиСРазделителями(ТЗ, Символы.ПС);
	КолСтрок = МассивСтрок.Количество();
	Для индСтрока = 0 По КолСтрок - 1 Цикл
		
		стрСтрока = МассивСтрок[индСтрока];
		
		Если Лев(СокрЛ(стрСтрока), 2)	= "//" Тогда
			МассивФМС.Добавить(Новый Структура("Строка, Цвет", стрСтрока, Цвет_Комментарии));
		ИначеЕсли Лев(СокрЛ(стрСтрока), 1)	= "|" И Прав(СокрП(стрСтрока),2) <> """;" Тогда
			МассивФМС.Добавить(Новый Структура("Строка, Цвет", стрСтрока, Цвет_Число));
		Иначе
			ПроверяемаяСтрока = ВРег(стрСтрока);
			
			МассивСлов = ПолучитьСписокСлов(стрСтрока);
			КолСлов = МассивСлов.Количество();
			Для индСлово = 0 По КолСлов - 1 Цикл
				
				стрСлово = МассивСлов[индСлово];
				
				Если ТипЗнч(стрСлово) = Тип("Структура") Тогда
					МассивФМС.Добавить(стрСлово);
					Продолжить;
				КонецЕсли;
				
				ПроверяемоеСлово = ВРег(стрСлово);
				
				Если форматирование_ЭтоКлючевоеСлово(ПроверяемоеСлово) Тогда
					МассивФМС.Добавить(Новый Структура("Строка, Цвет", стрСлово, Цвет_КлючевыеСлова));
				ИначеЕсли форматирование_ЭтоЧисло(стрСлово) Тогда
					МассивФМС.Добавить(Новый Структура("Строка, Цвет", стрСлово, Цвет_Число));
				Иначе  
					МассивФМС.Добавить(Новый Структура("Строка, Цвет", стрСлово, Цвет_Текст));
				КонецЕсли; 
				
			КонецЦикла;
			
		КонецЕсли; 
		
		Если индСтрока < КолСтрок - 1 Тогда
			МассивФМС.Добавить(Новый Структура("Строка, Цвет", Символы.ПС, ""));
		КонецЕсли; 
		
	КонецЦикла; 
	
	Если МассивФМС.Количество() = 0 Тогда
		Возврат Новый ФорматированнаяСтрока("");
	КонецЕсли;
	
	ФМС_Строка = "";
	Цвет_Текст = WebЦвета.Черный;
	ПредЦвет = МассивФМС[0].Цвет;
	Если ПредЦвет = "" Тогда
		ПредЦвет = Цвет_Текст;
	КонецЕсли;
	ФМС = Новый ФорматированнаяСтрока("");
	МассивФМСРез= Новый Массив;
	
	Для Каждого стрСлово Из МассивФМС Цикл
		Если стрСлово.Цвет = "" ИЛИ ПредЦвет = стрСлово.Цвет Тогда
			ФМС_Строка = ФМС_Строка + стрСлово.Строка;	
		Иначе
			Если ПредЦвет = Цвет_Текст Тогда
				МассивФМСРез.Добавить(ФМС_Строка);
			Иначе
				МассивФМСРез.Добавить(Новый ФорматированнаяСтрока(ФМС_Строка,,ПредЦвет));
			КонецЕсли;
			
			ФМС_Строка = стрСлово.Строка;
			ПредЦвет = стрСлово.Цвет;
		КонецЕсли; 
	КонецЦикла;
	Если ПредЦвет = Цвет_Текст Тогда
		МассивФМСРез.Добавить(ФМС_Строка);
	Иначе
		МассивФМСРез.Добавить(Новый ФорматированнаяСтрока(ФМС_Строка,,ПредЦвет));
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(МассивФМСРез);
	
КонецФункции

Функция ИзСтрокиСРазделителями(Знач вхСтр, вхРазделитель)
	
	Массив = Новый Массив;
	вхСтр = СтрЗаменить(вхСтр, вхРазделитель, Символы.ПС);
	ЧислоСтрок = СтрЧислоСтрок(вхСтр);
	Для Счетчик = 1 По ЧислоСтрок Цикл 
		Массив.Добавить(СтрПолучитьСтроку(вхСтр, Счетчик));
	КонецЦикла;
	Возврат Массив;
	
КонецФункции

Функция ПолучитьСписокСлов(Знач вхСтр)
	
	Цвет_КлючевыеСлова = WebЦвета.Красный;
	Цвет_Строка = WebЦвета.Черный;
	
	МассивРезультат = Новый Массив;
	
	ТекущееСлово = "";
	СтрДлинаСтр = СтрДлина(вхСтр);
	
	Для Поз = 1 По СтрДлинаСтр Цикл
		ТекущийСимвол = Сред(вхСтр, Поз, 1);
		
		Если ТекущийСимвол = " " Тогда
			
			Если ЗначениеЗаполнено(ТекущееСлово) Тогда
				МассивРезультат.Добавить(ТекущееСлово);
				ТекущееСлово = "";
			КонецЕсли;
			
			Пока ТекущийСимвол = " " и Поз <= СтрДлинаСтр Цикл
				ТекущееСлово = ТекущееСлово + Символы.НПП;
				Поз = Поз + 1;
				ТекущийСимвол = Сред(вхСтр, Поз, 1);
			КонецЦикла;
			МассивРезультат.Добавить(Новый Структура("Строка, Цвет", ТекущееСлово, ""));
			
			ТекущееСлово = "";
			
		КонецЕсли;
		
		Если Найти(",();=+-*<>'", ТекущийСимвол) > 0 Тогда
			
			Если ЗначениеЗаполнено(ТекущееСлово) Тогда
				МассивРезультат.Добавить(ТекущееСлово);
			КонецЕсли;
			
			Если ТекущийСимвол = ">" Тогда
				МассивРезультат.Добавить(Новый Структура("Строка, Цвет", "&gt;", Цвет_КлючевыеСлова));  //Баг платформы
			Иначе
				МассивРезультат.Добавить(Новый Структура("Строка, Цвет", ТекущийСимвол, Цвет_КлючевыеСлова));
			КонецЕсли; 
			
			ТекущееСлово = "";
			
		ИначеЕсли ТекущийСимвол = """" ИЛИ  ТекущийСимвол = "|" Тогда
			
			Если ЗначениеЗаполнено(ТекущееСлово) Тогда
				МассивРезультат.Добавить(ТекущееСлово);
			КонецЕсли;
			
			ТекущееСлово = ТекущийСимвол;
			Поз = Поз + 1;
			ТекущийСимвол = Сред(вхСтр, Поз, 1);
			
			Пока ТекущийСимвол <> "|" и ТекущийСимвол <> """" и Поз <= СтрДлинаСтр Цикл
				ТекущееСлово = ТекущееСлово + ТекущийСимвол;
				Поз = Поз + 1;
				ТекущийСимвол = Сред(вхСтр, Поз, 1);
			КонецЦикла;
			МассивРезультат.Добавить(Новый Структура("Строка, Цвет", ТекущееСлово + ТекущийСимвол, Цвет_Строка));
			
			ТекущееСлово = "";
			
		Иначе
			
			ТекущееСлово = ТекущееСлово + ТекущийСимвол;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекущееСлово) Тогда
		МассивРезультат.Добавить(ТекущееСлово);
	КонецЕсли; 
	
	Возврат МассивРезультат;
	
КонецФункции

Функция форматирование_ЭтоКлючевоеСлово(стрСлово)
	
	ПроверяемоеСлово = СокрЛП(стрСлово);
	
	СписокКлючевыхСлов = ";IF;ЕСЛИ;THEN;ТОГДА;ELSIF;ИНАЧЕЕСЛИ;ELSE;ИНАЧЕ;ENDIF;КОНЕЦЕСЛИ;DO;ЦИКЛ;FOR;ДЛЯ;TO;ПО;EACH;КАЖДОГО;IN;ИЗ;WHILE;ПОКА;ENDDO;КОНЕЦЦИКЛА;PROCEDURE;ПРОЦЕДУРА;ENDPROCEDURE;КОНЕЦПРОЦЕДУРЫ;FUNCTION;ФУНКЦИЯ;ENDFUNCTION;КОНЕЦФУНКЦИИ;VAR;ПЕРЕМ;EXPORT;ЭКСПОРТ;GOTO;ПЕРЕЙТИ;AND;И;OR;ИЛИ;NOT;НЕ;VAL;ЗНАЧ;BREAK;ПРЕРВАТЬ;CONTINUE;ПРОДОЛЖИТЬ;RETURN;ВОЗВРАТ;TRY;ПОПЫТКА;EXCEPT;ИСКЛЮЧЕНИЕ;ENDTRY;КОНЕЦПОПЫТКИ;RAISE;ВЫЗВАТЬИСКЛЮЧЕНИЕ;FALSE;ЛОЖЬ;TRUE;ИСТИНА;UNDEFINED;НЕОПРЕДЕЛЕНО;NULL;NEW;НОВЫЙ;EXECUTE;ВЫПОЛНИТЬ;";
	Возврат Найти(СписокКлючевыхСлов, ";"+ПроверяемоеСлово+";");
	
КонецФункции

Функция форматирование_ЭтоЧисло(стрСлово)
	
	ЛевСимв = Лев(стрСлово, 1);
	Если ЛевСимв >= "0" и ЛевСимв <= "9" Тогда
		Возврат Истина; 
	КонецЕсли; 
	
	Возврат Ложь; 
	
КонецФункции
#КонецОбласти


&НаКлиенте
Процедура ДляДокумента(Команда)
	ДляДокумента = Истина;
	ДляСправочника = Ложь;
	ДляРегистра = Ложь;
	ЗаполнитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ДляРегистра(Команда)
	ДляДокумента = Ложь;
	ДляСправочника = Ложь;
	ДляРегистра = Истина;
	ЗаполнитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ДляСправочника(Команда)
	ДляДокумента = Ложь;
	ДляСправочника = Истина;
	ДляРегистра = Ложь;
	ЗаполнитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура КодДляВыполненияПриИзменении(Элемент)
	КодДляВыполнения.УстановитьФорматированнуюСтроку(ПолучитьРаскрашенныйТекст(КодДляВыполнения.ПолучитьТекст()));
КонецПроцедуры
