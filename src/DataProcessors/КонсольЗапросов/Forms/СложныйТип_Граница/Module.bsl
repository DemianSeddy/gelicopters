
&НаКлиенте
Процедура ЗначениеВФормеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ЗначениеВФорме) Тогда
		м = Новый Массив;
		м.Добавить(ТипЗнч(ЗначениеВФорме));
		Элементы.ЗначениеВФорме.ОграничениеТипа = Новый ОписаниеТипов(м);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидГраницыВФорме = "Исключая";
	Если Параметры.Свойство("Значение") И ЗначениеЗаполнено(Параметры.Значение) Тогда
		ПараметрЗначение = ЗначениеИзСтрокиВнутр(Параметры.Значение);
		Если ЗначениеЗаполнено(ПараметрЗначение) Тогда
			ЗначениеВФорме = ПараметрЗначение.Значение;
			ВидГраницыВФорме = ПараметрЗначение.ВидГраницы;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть(Команда)
	ПараметрыПередачи = ПолучитьЗначениеСервер(ЗначениеВФорме, ВидГраницыВФорме);
	Закрыть(ПараметрыПередачи); 
	Владелец = ЭтотОбъект.ВладелецФормы;
	Владелец.Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеСервер(ЗначениеВФорме, ВидГраницыВФорме)

	Если НЕ ЗначениеЗаполнено(ЗначениеВФорме) Тогда
		Возврат Новый Структура("Значение, Представление", ЗначениеВСтрокуВнутр(Неопределено), "");
	КонецЕсли;
	
	Если ВидГраницыВФорме = "Включая" Тогда
		Значение = Новый Граница(ЗначениеВФорме, ВидГраницы.Включая);
	Иначе
		Значение = Новый Граница(ЗначениеВФорме, ВидГраницы.Исключая);
	КонецЕсли;
	
	ПредставлениеЗначения = "" + ЗначениеВФорме + "; " + ВидГраницыВФорме;
	Возврат Новый Структура("Значение, Представление", ЗначениеВСтрокуВнутр(Значение), ПредставлениеЗначения);
	
КонецФункции 

&НаКлиенте
Процедура ВидГраницыОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеВФормеОчистка(Элемент, СтандартнаяОбработка)
	Элементы.ЗначениеВФорме.ОграничениеТипа = Новый ОписаниеТипов();
КонецПроцедуры
