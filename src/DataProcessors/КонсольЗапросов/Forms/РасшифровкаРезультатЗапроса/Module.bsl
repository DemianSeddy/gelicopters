&НаСервере
Процедура УдалитьЭлементыФормы_КолонкиТаблицы(ЭлементТаблица) Экспорт 

	СписокСущКолонок = ЭлементТаблица.ПодчиненныеЭлементы;
	СписокИменКолонок = Новый СписокЗначений;
	Для Каждого Колонка Из СписокСущКолонок Цикл
		СписокИменКолонок.Добавить(Колонка.Имя);
	КонецЦикла; 
	
	Для Каждого Колонка Из СписокИменКолонок Цикл
		Элементы.Удалить(Элементы.Найти(Колонка.Значение));
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура УдалитьДобавитьКолонки_ТаблицаЗначений(СоздаваемаяТаблицаЗначений, ИмяРеквизитаФормы, ИмяЭлементаФормы) Экспорт

	ЭлементТаблица = Элементы[ИмяЭлементаФормы];
	УдалитьЭлементыФормы_КолонкиТаблицы(ЭлементТаблица);
	          
	МассивНовыхРеквизитов = Новый Массив;
	МассивУдаляемыхРеквизитов = Новый Массив;

	Для Каждого Колонка Из СоздаваемаяТаблицаЗначений.Колонки Цикл
		НовыйТип = Новый ОписаниеТипов(Колонка.ТипЗначения,,"Null");
		
		МассивНовыхРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, НовыйТип, ИмяРеквизитаФормы, Колонка.Заголовок));
	КонецЦикла;
	
	МассивСуществующихРеквизитов = ПолучитьРеквизиты(ИмяРеквизитаФормы);
	Для Каждого УдаляемыйРеквизит Из МассивСуществующихРеквизитов Цикл
		МассивУдаляемыхРеквизитов.Добавить(УдаляемыйРеквизит.Путь+"."+УдаляемыйРеквизит.Имя);
	КонецЦикла; 
	
	ИзменитьРеквизиты(МассивНовыхРеквизитов, МассивУдаляемыхРеквизитов);
	
	ЗначениеВДанныеФормы(СоздаваемаяТаблицаЗначений, ЭтаФорма[ИмяРеквизитаФормы]);
	
	Для Каждого Колонка Из СоздаваемаяТаблицаЗначений.Колонки Цикл
	
		КолонкаИмя = Колонка.Имя;
		
		ЭлементКолонка = Элементы.Добавить(ИмяЭлементаФормы + "_" + КолонкаИмя, Тип("ПолеФормы"), ЭлементТаблица);
		ЭлементКолонка.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементКолонка.ПутьКДанным = ИмяРеквизитаФормы + "." + КолонкаИмя;
	
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресХранилища") Тогда
		
		ИмяКолонки = Параметры.ИмяКолонки;
		Заголовок = "Колонка """ + ИмяКолонки + """ (Результат запроса)";
		
		РезультатЗапроса = ПолучитьИзВременногоХранилища(Параметры.АдресХранилища);
		ИндексСтроки = Параметры.ИндексСтроки;
		
		Если ТипЗнч(РезультатЗапроса) = Тип("ДеревоЗначений") Тогда
			ИндексРодителя = Параметры.ИндексРодителя;
			Если ИндексРодителя > РезультатЗапроса.Строки.Количество() - 1 Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			ТЗ = РезультатЗапроса.Строки.Получить(ИндексРодителя).Строки.Получить(ИндексСтроки)[ИмяКолонки];
			УдалитьДобавитьКолонки_ТаблицаЗначений(ТЗ, "РезультатЗапросаТаб","РезультатЗапросаТаб");
		Иначе
			Если ИндексСтроки > РезультатЗапроса.Количество() - 1 Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			ТЗ = РезультатЗапроса.Получить(ИндексСтроки)[ИмяКолонки];
			УдалитьДобавитьКолонки_ТаблицаЗначений(ТЗ, "РезультатЗапросаТаб","РезультатЗапросаТаб");
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗапросаТабВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТД = Элемент.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат; 
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТаблицаИмя = Элемент.Имя;
	ПолеИмя = Поле.Имя;
	
	ИмяКолонки = СтрЗаменить(ПолеИмя, ТаблицаИмя + "_", "");
	СодержимоеЯчейки = ТД[ИмяКолонки];
	
	ПоказатьЗначение(, СодержимоеЯчейки);

КонецПроцедуры
