
&НаКлиенте
Процедура СсылкаПриИзменении(Элемент)
	Если не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Дата = ДатаОбъект(Ссылка);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатаОбъект(Ссылка)
	Возврат Ссылка.Дата;
КонецФункции 

&НаКлиенте
Процедура ЗначениеВФормеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		м = Новый Массив;
		м.Добавить(ТипЗнч(Ссылка));
		Элементы.Ссылка.ОграничениеТипа = Новый ОписаниеТипов(м);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Момент = Неопределено;
	Если Параметры.Свойство("Значение") И ЗначениеЗаполнено(Параметры.Значение) Тогда
		Момент = ЗначениеИзСтрокиВнутр(Параметры.Значение);
	ИначеЕсли Параметры.Свойство("АдресХранилища") Тогда
		
		Заголовок = "Момент времени";
		Элементы.Ссылка.ТолькоПросмотр = Истина;
		Элементы.Ссылка.КнопкаОткрытия = Истина;
		Элементы.Дата.ТолькоПросмотр = Истина;
		
		ИмяКолонки = Параметры.ИмяКолонки;
		
		РезультатЗапроса = ПолучитьИзВременногоХранилища(Параметры.АдресХранилища);
		ИндексСтроки = Параметры.ИндексСтроки;
		
		Если ТипЗнч(РезультатЗапроса) = Тип("ДеревоЗначений") Тогда
			ИндексРодителя = Параметры.ИндексРодителя;
			Если ИндексРодителя > РезультатЗапроса.Строки.Количество() - 1 Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			Момент = РезультатЗапроса.Строки.Получить(ИндексРодителя).Строки.Получить(ИндексСтроки)[ИмяКолонки];
		Иначе
			Если ИндексСтроки > РезультатЗапроса.Количество() - 1 Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			Момент = РезультатЗапроса.Получить(ИндексСтроки)[ИмяКолонки];
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Момент) Тогда
		Ссылка = Момент.Ссылка;
		Дата = Момент.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть(Команда)
	ПараметрыПередачи = ПолучитьЗначениеСервер(Ссылка, Дата);
	Закрыть(ПараметрыПередачи); 
	Владелец = ЭтотОбъект.ВладелецФормы;
	Владелец.Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеСервер(Ссылка, Дата)
	
	Параметры = Новый Массив(2);
	Параметры[0] = Дата;
	Параметры[1] = Ссылка;
	Значение = Новый (Тип("МоментВремени"),Параметры);
	
	ПредставлениеЗначения = "" + Дата + "; " + Ссылка;
	Возврат Новый Структура("Значение, Представление", ЗначениеВСтрокуВнутр(Значение), ПредставлениеЗначения);
	
КонецФункции 

&НаКлиенте
Процедура ЗначениеВФормеОчистка(Элемент, СтандартнаяОбработка)
	Элементы.Ссылка.ОграничениеТипа = Новый ОписаниеТипов();
КонецПроцедуры
